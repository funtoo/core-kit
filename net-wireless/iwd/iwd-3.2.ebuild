# Distributed under the terms of the GNU General Public License v2

EAPI=7
inherit flag-o-matic linux-info systemd

DESCRIPTION="Wireless daemon for linux"
HOMEPAGE="https://git.kernel.org/pub/scm/network/wireless/iwd.git/"
SRC_URI="https://mirrors.edge.kernel.org/pub/linux/network/wireless/iwd-3.2.tar.xz -> iwd-3.2.tar.xz"

LICENSE="GPL-2"
SLOT="0"
IUSE="+client cpu_flags_x86_aes cpu_flags_x86_ssse3 +crda +monitor ofono standalone systemd wired"
KEYWORDS="*"
MYRST2MAN="RST2MAN=:"


DEPEND="
	sys-apps/dbus
	client? ( sys-libs/readline:0= )
	~dev-libs/ell-0.71
"

RDEPEND="
	${DEPEND}
	net-wireless/wireless-regdb
	crda? ( net-wireless/crda )
	standalone? (
		systemd? ( sys-apps/systemd )
		!systemd? ( virtual/resolvconf )
	)
"

BDEPEND="
	virtual/pkgconfig
"

pkg_setup() {
	CONFIG_CHECK="
		~ASYMMETRIC_KEY_TYPE
		~ASYMMETRIC_PUBLIC_KEY_SUBTYPE
		~CFG80211
		~CRYPTO_AES
		~CRYPTO_CBC
		~CRYPTO_CMAC
		~CRYPTO_DES
		~CRYPTO_ECB
		~CRYPTO_HMAC
		~CRYPTO_MD4
		~CRYPTO_MD5
		~CRYPTO_RSA
		~CRYPTO_SHA1
		~CRYPTO_SHA256
		~CRYPTO_SHA512
		~CRYPTO_USER_API_HASH
		~CRYPTO_USER_API_SKCIPHER
		~KEY_DH_OPERATIONS
		~PKCS7_MESSAGE_PARSER
		~RFKILL
		~X509_CERTIFICATE_PARSER
	"
	if use crda;then
		CONFIG_CHECK="${CONFIG_CHECK} ~CFG80211_CRDA_SUPPORT"
		WARNING_CFG80211_CRDA_SUPPORT="REGULATORY DOMAIN PROBLEM: please enable CFG80211_CRDA_SUPPORT for proper regulatory domain support"
	fi

	if use amd64;then
		CONFIG_CHECK="${CONFIG_CHECK} ~CRYPTO_DES3_EDE_X86_64"
		WARNING_CRYPTO_DES3_EDE_X86_64="CRYPTO_DES3_EDE_X86_64: enable for increased performance"
	fi

	if use cpu_flags_x86_aes;then
		CONFIG_CHECK="${CONFIG_CHECK} ~CRYPTO_AES_NI_INTEL"
		WARNING_CRYPTO_AES_NI_INTEL="CRYPTO_AES_NI_INTEL: enable for increased performance"
	fi

	if use cpu_flags_x86_ssse3 && use amd64; then
		CONFIG_CHECK="${CONFIG_CHECK} ~CRYPTO_SHA1_SSSE3 ~CRYPTO_SHA256_SSSE3 ~CRYPTO_SHA512_SSSE3"
		WARNING_CRYPTO_SHA1_SSSE3="CRYPTO_SHA1_SSSE3: enable for increased performance"
		WARNING_CRYPTO_SHA256_SSSE3="CRYPTO_SHA256_SSSE3: enable for increased performance"
		WARNING_CRYPTO_SHA512_SSSE3="CRYPTO_SHA512_SSSE3: enable for increased performance"
	fi

	if use kernel_linux && kernel_is -ge 4 20; then
		CONFIG_CHECK="${CONFIG_CHECK} ~PKCS8_PRIVATE_KEY_PARSER"
	fi

	check_extra_config

	if ! use crda; then
		if use kernel_linux && kernel_is -lt 4 15; then
			ewarn "POSSIBLE REGULATORY DOMAIN PROBLEM:"
			ewarn "Regulatory domain support for kernels older than 4.15 requires crda."
		fi
		if linux_config_exists && linux_chkconfig_builtin CFG80211 &&
			[[ $(linux_chkconfig_string EXTRA_FIRMWARE) != *regulatory.db* ]]
		then
			ewarn ""
			ewarn "REGULATORY DOMAIN PROBLEM:"
			ewarn "With CONFIG_CFG80211=y (built-in), the driver won't be able to load regulatory.db from"
			ewarn " /lib/firmware, resulting in broken regulatory domain support.  Please set CONFIG_CFG80211=m"
			ewarn " or add regulatory.db and regulatory.db.p7s to CONFIG_EXTRA_FIRMWARE."
			ewarn ""
		fi
	fi
}

src_configure() {
	append-cflags "-fsigned-char"
	local myeconfargs=(
		--sysconfdir="${EPREFIX}"/etc/iwd --localstatedir="${EPREFIX}"/var
		$(use_enable client)
		$(use_enable monitor)
		$(use_enable ofono)
		$(use_enable wired)
		--enable-systemd-service
		--with-systemd-unitdir="$(systemd_get_systemunitdir)"
		--with-systemd-modloaddir="${EPREFIX}/usr/lib/modules-load.d"
		--with-systemd-networkdir="$(systemd_get_utildir)/network"
		--enable-external-ell
	)
	econf "${myeconfargs[@]}"
}

src_compile() {
	emake ${MYRST2MAN}
}

src_install() {
	emake DESTDIR="${D}" ${MYRST2MAN} install
	keepdir /var/lib/${PN}

	newinitd "${FILESDIR}/iwd.initd-r1" iwd

	if use wired;then
		newinitd "${FILESDIR}/ead.initd" ead
	fi

	if use standalone ; then
		local iwdconf="${ED}/etc/iwd/main.conf"
		dodir /etc/iwd
		echo "[General]" > "${iwdconf}"
		echo "EnableNetworkConfiguration=true" >> "${iwdconf}"
		echo "[Network]" >> "${iwdconf}"
		echo "NameResolvingService=$(usex systemd systemd resolvconf)" >> "${iwdconf}"
		dodir /etc/conf.d
		echo "rc_provide=\"net\"" > ${ED}/etc/conf.d/iwd
	fi
}