# Distributed under the terms of the GNU General Public License v2

EAPI=7

PYTHON_COMPAT=( python3+ )
DISTUTILS_OPTIONAL=1

inherit distutils-r1 libtool usr-ldscript

MY_P=${P/_}
DESCRIPTION="Password Checking Library"
HOMEPAGE="https://github.com/cracklib/cracklib/"
SRC_URI="{{artifacts[0].src_uri}} {{artifacts[1].src_uri}}"

LICENSE="LGPL-2.1"
SLOT="0"
KEYWORDS="*"
IUSE="nls python static-libs zlib"
REQUIRED_USE="python? ( ${PYTHON_REQUIRED_USE} )"

RDEPEND="python? ( ${PYTHON_DEPS} )
	zlib? ( >=sys-libs/zlib-1.2.8-r1:= )"
DEPEND="${RDEPEND}
	nls? ( virtual/libintl )
	python? (
		dev-python/setuptools[${PYTHON_USEDEP}]
	)"
BDEPEND="nls? ( sys-devel/gettext )"

S="${WORKDIR}/${MY_P}"

do_python() {
	use python || return 0
	pushd python > /dev/null || die
	distutils-r1_src_${EBUILD_PHASE}
	popd > /dev/null
}

pkg_setup() {
	# workaround #195017
	if has unmerge-orphans ${FEATURES} && has_version "<${CATEGORY}/${PN}-2.8.10" ; then
		eerror "Upgrade path is broken with FEATURES=unmerge-orphans"
		eerror "Please run: FEATURES=-unmerge-orphans emerge cracklib"
		die "Please run: FEATURES=-unmerge-orphans emerge cracklib"
	fi
}

src_prepare() {
	eapply_user
	elibtoolize #269003
	do_python
}

src_configure() {
	local myeconfargs=(
		# use /usr/lib so that the dictionary is shared between ABIs
		--with-default-dict='/usr/lib/cracklib_dict'
		--without-python
		$(use_enable nls)
		$(use_enable static-libs static)
	)
	export ac_cv_header_zlib_h=$(usex zlib)
	export ac_cv_search_gzopen=$(usex zlib -lz no)
	ECONF_SOURCE="${S}" econf "${myeconfargs[@]}"
}

src_compile() {
	default
	do_python
	# Remove entries longer than 40 characters -- there are a few weird ones in the file:
	sed -i -e '/^.\{40\}./d' ${WORKDIR}/cracklib-words-${PV} || die
}

rc_test() {
	# Make sure we load the freshly built library
	LD_LIBRARY_PATH="${BUILD_DIR}/lib/.libs" do_python
}

python_test() {
	${EPYTHON} -m unittest test_cracklib || die "Tests fail with ${EPYTHON}"
}

src_install() {
	default
	# move shared libs to /
	gen_usr_ldscript -a crack

	do_python
	einstalldocs
	find "${ED}" -type f -name "*.la" -delete || die
	rm -r "${ED}"/usr/share/cracklib || die

	insinto /usr/share/dict
	doins dicts/cracklib-small
	insinto /usr/share/dict
	newins ${WORKDIR}/cracklib-words-${PV} cracklib-words
}

pkg_postinst() {
	ebegin "Regenerating cracklib dictionary"
	${EROOT}/usr/sbin/cracklib-format ${EROOT}/usr/share/dict/* | ${EROOT}/usr/sbin/cracklib-packer ${ROOT}/usr/lib/cracklib_dict
	eend $?
}
